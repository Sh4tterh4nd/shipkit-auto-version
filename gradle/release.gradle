// docs: https://plugins.gradle.org/docs/publish-plugin
gradlePlugin {
    plugins {
        autoVersion {
            id = 'org.shipkit.shipkit-auto-version'
            implementationClass = 'org.shipkit.auto.version.AutoVersionPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/shipkit/org.shipkit.shipkit-auto-version'
    vcsUrl = 'https://github.com/shipkit/org.shipkit.shipkit-auto-version.git'
    plugins {
        autoVersion {
            displayName = 'Shipkit auto-version plugin'
            description = 'Increments version automatically for easy releases from CI'
            tags = ['ci', 'shipkit', 'releases']
        }
    }
}

ext.'gradle.publish.key' = System.getenv('GRADLE_PUBLISH_KEY')
ext.'gradle.publish.secret' = System.getenv('GRADLE_PUBLISH_SECRET')

if (ext.'gradle.publish.key' && ext.'gradle.publish.secret') {
    println "Gradle Plugin Portal environment variables: " +
            "key=${ext.'gradle.publish.key'.substring(0, 3)}, secret=${ext.'gradle.publish.secret'.substring(0, 3)}"
}

def tagName = "v$version"

task createTag {
    description = "Creates a '$tagName' annotated tag"
    doLast {
        println "Creating tag $tagName"
        exec { commandLine "git", "config", "user.email", "shipkit.org@gmail.com" }
        exec { commandLine "git", "config", "user.name", "shipkit" }
        exec { commandLine "git", "tag", "-a", "-m", "Release $tagName", tagName }
        exec { commandLine "git", "push", "origin", tagName }
        println "Created tag $tagName"
    }
}

tasks.named("generateChangelog") {
    previousRevision = "v" + project.ext.'shipkit-auto-version.previous-version'
    readOnlyToken = "a0a4c0f41c200f7c653323014d6a72a127764e17"
    repository = "shipkit/shipkit-changelog"
}

tasks.named("githubRelease") {
    dependsOn tasks.named("generateChangelog")
    repository = "shipkit/shipkit-changelog"
    changelog = tasks.named("generateChangelog").get().outputFile
    writeToken = System.getenv("GH_WRITE_TOKEN")
}