apply plugin: "java-gradle-plugin"
apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.shipkit.shipkit-auto-version"
apply plugin: "org.shipkit.shipkit-changelog"
apply plugin: "org.shipkit.shipkit-gh-release"

// docs: https://plugins.gradle.org/docs/publish-plugin
gradlePlugin {
    plugins {
        autoVersion {
            id = 'org.shipkit.shipkit-auto-version'
            implementationClass = 'org.shipkit.auto.version.AutoVersionPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/shipkit/org.shipkit.shipkit-auto-version'
    vcsUrl = 'https://github.com/shipkit/org.shipkit.shipkit-auto-version.git'
    plugins {
        autoVersion {
            displayName = 'Shipkit auto-version plugin'
            description = 'Increments version automatically for easy releases from CI'
            tags = ['ci', 'shipkit', 'releases']
        }
    }
}

ext.'gradle.publish.key' = System.getenv('GRADLE_PUBLISH_KEY')
ext.'gradle.publish.secret' = System.getenv('GRADLE_PUBLISH_SECRET')

if (ext.'gradle.publish.key' && ext.'gradle.publish.secret') {
    println "Gradle Plugin Portal environment variables: " +
            "key=${ext.'gradle.publish.key'.substring(0, 3)}, secret=${ext.'gradle.publish.secret'.substring(0, 3)}"
}

tasks.named("generateChangelog") {
    previousRevision = 'shipkit-auto-version.previous-tag'
    readOnlyToken = "a0a4c0f41c200f7c653323014d6a72a127764e17"
    repository = "shipkit/shipkit-auto-version"
}

tasks.named("githubRelease") {
    def genTask = tasks.named("generateChangelog").get()
    dependsOn genTask
    repository = genTask.repository
    changelog = genTask.outputFile
    writeToken = System.getenv("GH_WRITE_TOKEN")
}